apply plugin: 'com.android.application'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.dagger.hilt.android'

android {
    namespace 'com.huhx0015.coffeenearby'
    compileSdkVersion 36
    defaultConfig {
        applicationId "com.huhx0015.coffeenearby"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def keysPropertiesFile = rootProject.file("keys.properties")
        def keysProperties = new Properties()
        if (keysPropertiesFile.exists()) {
            keysProperties.load(new FileInputStream(keysPropertiesFile))
        }

        buildConfigField "String", "YELP_API_KEY", "\"${keysProperties['YELP_API_KEY'] ?: ''}\""
        buildConfigField "String", "YELP_BASE_URL", "\"${keysProperties['YELP_BASE_URL'] ?: ''}\""
        buildConfigField "String", "YELP_CLIENT_ID", "\"${keysProperties['YELP_CLIENT_ID'] ?: ''}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        buildConfig = true
    }
}

ext {
  activity_version = "1.12.2"
  app_compat_version = "1.7.1"
  coil_version = "3.3.0"
  compose_activity_version = "1.12.2"
  compose_bom_version = "2026.01.00"
  constraint_layout_version = "2.2.1"
  core_ktx_version = "1.17.0"
  espresso_version = "3.7.0"
  fragment_version = "1.8.9"
  hilt_nav_compose_version = "1.3.0"
  junit_ext_version = "1.3.0"
  junit_version = "4.13.2"
  lifecycle_version = "2.10.0"
  moshi_version = "1.15.2"
  okhttp_bom_version = "5.3.2"
  retrofit_version = "3.0.0"
}

dependencies {

  // ANDROID JETPACK:
  implementation "androidx.activity:activity-ktx:$activity_version"
  implementation "androidx.appcompat:appcompat:$app_compat_version"
  implementation "androidx.core:core-ktx:$core_ktx_version"
  implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
  implementation "androidx.fragment:fragment-ktx:$fragment_version"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

  // COMPOSE:
  implementation platform("androidx.compose:compose-bom:$compose_bom_version")
  implementation "androidx.compose.animation:animation-graphics"
  implementation "androidx.compose.material3:material3"
  implementation "androidx.compose.ui:ui-tooling"
  implementation "androidx.compose.ui:ui-tooling-preview"
  implementation "androidx.compose.runtime:runtime-livedata"
  implementation "androidx.activity:activity-compose:$compose_activity_version"
  implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

  // COIL:
  implementation "io.coil-kt.coil3:coil:$coil_version"
  implementation "io.coil-kt.coil3:coil-svg:$coil_version"
  implementation "io.coil-kt.coil3:coil-compose:$coil_version"
  implementation "io.coil-kt.coil3:coil-network-okhttp:$coil_version"

  // HILT:
  implementation "com.google.dagger:hilt-android:$hilt_version"
  implementation "androidx.hilt:hilt-navigation-compose:$hilt_nav_compose_version"
  ksp "com.google.dagger:hilt-android-compiler:$hilt_version"
  ksp "com.google.dagger:hilt-compiler:$hilt_version"

  // KOTLIN:
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  // MOSHI:
  implementation "com.squareup.moshi:moshi:$moshi_version"
  implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
  implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
  ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

  // OKHTTP:
  implementation platform("com.squareup.okhttp3:okhttp-bom:$okhttp_bom_version")
  implementation "com.squareup.okhttp3:okhttp"
  implementation "com.squareup.okhttp3:logging-interceptor"

  // RETROFIT:
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

  // ANDROID TEST:
  androidTestImplementation "androidx.test.ext:junit:$junit_ext_version"
  androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

  // TEST:
  testImplementation "junit:junit:$junit_version"
}
